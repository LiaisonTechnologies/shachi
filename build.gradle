apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'java-library-distribution'

configurations {
    provided
    provided.extendsFrom(compile)
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply from: file('gradle/buildscript.gradle'), to: buildscript
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        // Liaison Private Maven artifact repo (Nexus)
        maven { url releasesArtifactRepoURL }
        maven { url snapshotsArtifactRepoURL }
        maven { url thirdpartyArtifactRepoURL }
    }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/check.gradle')
apply from: file('gradle/license.gradle')
apply from: file('gradle/maven.gradle')

group = 'com.liaison'
version = '0.0.1-SNAPSHOT'

ext.libraryName = "liaison-hbase-${version}"
ext.verGuava = '18.0'
ext.verMockito = '1.10.19'
ext.verPhoenixCommons = '0.0.2-SNAPSHOT'
ext.verPool = '2.3'
ext.verQuasar  = '0.6.2'
ext.verTestNG  = '6.8.21'

ext.classifier = ':jdk8'

if (project.hasProperty('env') && project.getProperty('env') == 'mapr') {
    repositories {
        mavenCentral()
        maven {
            url "http://repository.mapr.com/maven/"
        }
    }
    // ext.verHBase = '0.98.7-mapr-1501'
    ext.verHBase = '0.98.9-mapr-1503-m7-4.1.0'
    ext.verZookeeper = '3.4.5-mapr-1406'
} else {
    // ext.verHBase = '0.98.7-hadoop2'
    ext.verHBase = '0.98.9-hadoop2'
    ext.verZookeeper = '3.4.5'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': version
    }
}

dependencies {
    compile "com.liaison.phoenix:phoenix-commons:0.0.1-SNAPSHOT"
    compile "com.google.guava:guava:${verGuava}"
    compile "co.paralleluniverse:quasar-core:${verQuasar}${classifier}"
    provided "org.apache.hbase:hbase-client:${verHBase}"
    provided "org.apache.hbase:hbase-common:${verHBase}"
    compile "org.apache.commons:commons-pool2:${verPool}"
    testCompile "org.testng:testng:${verTestNG}"
    testCompile "org.mockito:mockito-all:${verMockito}"
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from {
        configurations.compile.collect {
            logger.info "Including compile dependency in fat JAR: ${it}"
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

test {
    useTestNG()
}

distributions {
    main {
        baseName = "${libraryName}"
    }
}

uploadArchives {
    repositories {
       flatDir {
           dirs 'repos'
       }
    }
}

eclipse.classpath.plusConfigurations += configurations.provided
plugins.withType(IdeaPlugin) {
    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }
}