apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

/*
 * Copyright Â© 2016 Liaison Technologies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at:
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

// Configure the shadow jar task
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

// inject javaagent into the distribution
applicationDefaultJvmArgs = [
        "-javaagent:\$APP_HOME/lib/quasar-core-$quasarVersion"+".jar",
        "-server",
        "-Xmx1024m",
        '-Dco.paralleluniverse.fibers.verifyInstrumentation=false'
]


// $ symbol gets escaped in script anyway:( so we need to replace it.

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('\\$APP_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('\$APP_HOME', '%APP_HOME%')

        if (project.hasProperty('env') && project.getProperty('env') == 'mapr') {
            unixScript.text = unixScript.text.replace('"\$CLASSPATH"', '"\$CLASSPATH:`hbase classpath`"')
            windowsScript.text = windowsScript.text.replace('"%CLASSPATH%"', '"%CLASSPATH%;`hbase classpath`"')
        }

    }
}

distZip {
    into(project.name) {
        from 'conf/*'
        include 'conf/*'
    }

    into(project.name) {
        from 'bin/*'
        include 'bin/*'
    }
}

// don't know why there is old version of netty, simply exclude here.
applicationDistribution.with {
    exclude 'netty-3.2.*.jar'
}

applicationDistribution.from('conf') {
    include '*'
    into "conf"
}

applicationDistribution.from('bin') {
    include '*'
    into "bin"
    fileMode 0755
}



